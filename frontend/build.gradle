plugins {
    id 'base'
    id "com.moowork.node" version "1.3.1"
}

node {
    // Version of node to use.
    version = '12.9.1'
    // Version of npm to use.
    npmVersion = '6.10.2'
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir 'build/dist'
}

//TODO include version?
task packageNpmApp(type: Zip) {
    dependsOn npm_run_build
    baseName 'myturk-frontend'
    extension 'jar'
    destinationDir file("${projectDir}/build/libs/")
    from('build/dist') {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        into 'static'
    }
}

artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type "jar"
    }
}

assemble.dependsOn(packageNpmApp)